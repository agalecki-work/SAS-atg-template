/* Generates info on available repository assets */
/* Info on SAS datasets is generated by 05data-info.sas */

/*===== SAS SETUP starts */

/* Auto executed. Note the location of the file*/
filename setupc   "C:/Users/Public/__sas_session_info.sas";
%include setupc;


filename sassetup "&repo_path.__123setup.inc";
%include sassetup;

/*===== SAS SETUP ends ====== */


/*--- OUR SESSION STARTS --- */

options nocenter;
ods _all_ close;

/*=== Report on repository assets: macros, macro variables === */
ods html file = "02repo-assets-info.html"
         path = "&repo_path.reports";  /* path to `./reports` subfolder */
%include ods_init; /* Basic info about SAS session */


Title "List of selected macro variables (Program executed on &sysdate)";
data vmacro (drop = keepit);
 set sashelp.vmacro (drop = offset);
 * value = substr(value, 1, 85);
 keepit = 0;
 if scope ="GLOBAL" then keepit=1;
 if name in ("SYSPROCESSNAME","SYSSCPL","SYSUSERID","SYSVLONG4") then keepit =1;
 if keepit;
run;

proc print data=vmacro;
run; 

Title "List of user defined SAS macros in WORK.SASMACR";
data vcatalg;
  set sashelp.vcatalg (where= (libname='WORK' and memname='SASMACR'));
run;

proc print data=vcatalg;
 var objname objdesc;
run;

/*--- cleanup ---*/
%delete_datasets(vcatalg vmacro);
ods html close;

/*=== Report on datasets ===*/

ods html file = "02-repo-data-info.html"
         path = "&repo_path.reports/"
;
%include ods_init; /* Basic info about SAS session */

Title "List of defined SAS libraries (on &sysdate)";
data tmp;
  set sashelp.VLIBNAM (keep =libname path);
  if libname in ("SASHELP","MAPS","MAPSSAS", "MAPSGFK", "SASUSER") then delete;
  run;

proc print data = tmp;
run;

%delete_datasets(tmp); /*--- dataset `tmp` in `work` library not needed, therefore deleted ---*/


Title "List of datasets.";

data _vtable;
  set sashelp.vtable(where = (libname notin ("SASHELP", "MAPS","MAPSSAS", "MAPSGFK","MAPSUSER","SASUSER")));
run;

proc print data = _vtable;
var libname memname crdate nobs nvar memlabel;
run;

%delete_datasets(_vtable); 

Title "List of variables ";
data _vcolumn;
 set SASHELP.VCOLUMN(where = (libname notin ("SASHELP", "MAPS","MAPSSAS", "MAPSGFK","MAPSUSER","SASUSER")));
run;

proc print data=_vcolumn;
var libname memname varnum name type length label format informat sortedby; 
run;

/* cleanup */
%delete_datasets(_vcolumn); 
ods html close;

